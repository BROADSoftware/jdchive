/*
 * Copyright (C) 2016 BROADSoftware
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply plugin: 'eclipse'


repositories {
    jcenter()
    // mavenCentral()
}

configurations {
	compileAndJar
	compile.extendsFrom(compileAndJar)
}

// Refer to https://issues.gradle.org/browse/GRADLE-1010
eclipseJdt.doLast( {
	File f1 = file('.settings/org.eclipse.core.resources.prefs')
   	f1.write('eclipse.preferences.version=1\n')
   	f1.append('encoding/<project>=UTF-8')
	File f2 = file('.settings/org.eclipse.core.runtime.prefs')
   	f2.write('eclipse.preferences.version=1\n')
   	f2.append('line.separator=\\n')
})
	

dependencies {
	compile "org.slf4j:slf4j-api:$sl4jApiVersion"
	compile "org.slf4j:slf4j-log4j12:$sl4jLog4jVersion"
	compile "log4j:log4j:$log4jVersion"
	compile "org.apache.hive:hive-metastore:$hiveVersion"
	compile ("org.apache.hive:hive-exec:$hiveVersion") {
		exclude group: "org.pentaho", module: "pentaho-aggdesigner-algorithm" // Could not find org.pentaho:pentaho-aggdesigner-algorithm:5.1.5-jhyde.
		exclude group: "org.apache.curator", module: "apache-curator" // Archive for required library: '/..../.gradle/caches/modules-2/files-2.1/org.apache.curator/apache-curator/2.6.0/1bbbe3a8c191ec5a63ec2eaec7988d8292ea65d2/apache-curator-2.6.0.pom' in project 'jdchive' cannot be read or is not a valid ZIP file
	}
    compile "org.apache.hadoop:hadoop-common:$hadoopVersion"		
    compile "org.apache.hadoop:hadoop-mapreduce-client-core:$hadoopVersion"		
 	compileAndJar "net.sf.jopt-simple:jopt-simple:$joptVersion"
	compileAndJar "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"
	compileAndJar "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    //compileAndJar "org.yaml:snakeyaml:$snakeyamlVersion"
    //compileAndJar "com.esotericsoftware.yamlbeans:yamlbeans:$yamlbeansVersion"
	testCompile "org.apache.commons:commons-lang3:3.4"
    testCompile "junit:junit:4.12"
}


jar {
	exclude("log4j.xml")
	rename("log4j-jar.xml", "log4j.xml")
}


//create a single Jar with all dependencies
task uberJar(type: Jar) {
	manifest {
        attributes 'Implementation-Title': 'jdchive', 
        	'Implementation-Version': version,
        	'Main-Class': 'com.kappaware.jdchive.Main'
    }
    baseName = project.name + "_uber"
    from { configurations.compileAndJar.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}


